// To be run when the clothes workspace is the active project
() {
    ~ string[] TOPS = [ "vest", "blouse" ];
    ~ string[] BOTTOMS = [ "slacks", "shorts" ];
    ~ string[] SHOES = [ "simple" ];
    ~ string[] OUTFITS = [ "farmer-1", "gi" ];

    ~ int start_bottoms = #| TOPS;
    ~ int start_shoes = start_bottoms + (#| BOTTOMS)
    ~ int start_outfits = start_shoes + (#| SHOES);
    ~ int to_save = start_outfits + (#| OUTFITS);

    project clothes_workspace = $SE.get_project();
    layer clothes_layer = clothes_workspace.get_layer("Clothes");

    for (int i = 0; i < to_save; i++) {
        if (i < start_bottoms)
            save_clothing(clothes_layer, i, TOPS[i], "top");
        else if (i < start_shoes)
            save_clothing(clothes_layer, i, BOTTOMS[i - start_bottoms], "bottom");
        else if (i < start_outfits)
            save_clothing(clothes_layer, i, SHOES[i - start_shoes], "shoes");
        else
            save_clothing(clothes_layer, i, OUTFITS[i - start_outfits], "outfit");
    }
}

save_clothing(~ layer clothes_layer, ~ int i, ~ string id, ~ string article) {
    ~ image content = clothes_layer.get_cel(i);

    save_size(content, id, article, true);
    save_size(content, id, article, false);
}

save_size(~ image content, ~ string id, ~ string article, ~ bool isSmall) {
    image anim = animate(content, isSmall);

    ~ string art_folder = "../../bases/" + prefix(isSmall) + "-" + article + "/";
    ~ string program_folder = "../../../../../../Documents/Java/2022/tdsm/res/sprite_assets/pkmn/" + 
            prefix(isSmall) + "-" + article + "/";
    
    write_image(art_folder + id + ".png", anim);
    write_image(program_folder + id + ".png", anim);
}

animate(~ image source, ~ bool isSmall -> image) {
    ~ string FOLDER = "../../workspaces/";
    ~ image MAP = read_image(FOLDER + "t-body-map.png");
    ~ image ANIM = read_image(FOLDER + prefix(isSmall) + "-anim-mapping.png");

    return $Graphics.uv_mapping(source, MAP, ANIM);
}

prefix(~ bool isSmall -> string) -> isSmall ? "sm" : "av"
